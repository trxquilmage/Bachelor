title: DJPizza.Start
tags:
---
<<showcharactername Managers DJ Pizza>>
<<set $questIsActive to isquestactive("QuestAAB")>>
<<set $quest2IsActive to isquestactive("QuestAAC")>>
Hello.
<<if not isvisited("DJPizza","Start")>>
    [[DJPizza.FirstIntroduction]]
<<elseif $questIsActive>>
    [[DJPizza.ForgotAboutPlayer]]
<<elseif $quest2IsActive>>
    [[DJPizza.RetrySecretWord]]
<<else>>
    [[DJPizza.Idle]]
<<endif>>
===

title: DJPizza.FirstIntroduction
tags:
---
I'm the DJ for the evening.
DJ Pizza - book me if you are celebrating a 
wedding, a funeral or a birthday party!
Me and my pizza mobile will be right with you!
Say... what is your name?
<<setquest Managers QuestAAB>>
[[DJPizza.Introduction]]
===

title: DJPizza.Introduction
tags:
---
<<showicantsay Managers>>
<<displaypromptmenu Managers NameAAB>>
I'm looking for someone here...
<<set $cantsay to icantsay("DJPizza", "Nevermind")>>
<<set $name to react("name", "", "DJPizza")>>
<<if $name eq "Jordan">>
    Great! I was looking for you!
    <<completequest Managers QuestAAB>>
    <<setquest Managers QuestAAC>>
    [[DJPizza.ExplainSecretWord]]
<<else>>
    Oh, too bad, 
    you're not who I'm looking for.
<<endif>>
===

title: DJPizza.ForgotAboutPlayer
tags:
---
Oh? Have we met already?
[[DJPizza.Introduction]]
===

title: DJPizza.ExplainSecretWord
tags:
---
Oh, sorry... 
I need to check if it's really you.
[[DJPizza.SecretWord]]
===

title: DJPizza.RetrySecretWord
tags:
---
Sorry, I really need to make sure,
that you are Jordan...
[[DJPizza.SecretWord]]
===

title: DJPizza.SecretWord
tags:
---
<<showicantsay Managers>>
<<displaypromptmenu Managers AllAAA>>
What is the secret word we made out?
<<set $cantsay to icantsay("DJPizza", "ForgotWord")>>
<<set $secretWord to react("name", "", "DJPizza")>>
<<if $secretWord eq "Pineapple Bun">>
    [[DJPizza.TheDeal]]
<<elseif $secretWord eq "Pizza">>
    You think that's funny, huh? 
    My passion for pizza died about 13 years ago.
<<else>>
    Nah. Sorry, that's wrong.
    [[DJPizza.ForgotWord]]
<<endif>>
===

title: DJPizza.Idle
tags:
---
I'm so glad Parker found a home.
===

title: DJPizza.Nevermind
tags:
---
Okay nevermind, then...
===

title: DJPizza.ForgotWord
tags:
---
Did you forget the secret word?
Just check your email, again then.
And come back when you found it.
===


title: DJPizza.TheDeal
tags:
---
<<completequest Managers QuestAAC>>
Great. Sorry, I'm just careful.
<<showcharactername Managers Action>>
<i><color=grey>DJ Pizza hands you a small glass bowl.</i>
<i><color=grey>Inside is a goldfish, swimming around.</i>
<<showcharactername Managers DJPizza>>
His name is Parker. 
I already got your payment, so...
It was nice doing business with you. 
===

title: DJPizza.ICantSay
tags:
---
Hm.
<<onnodecomplete Managers>>
This has to be here so the dialogue doesnt end otherwise the dialogue will be over before the command is done and that causes problems.
===